import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.kotlin.android) apply false
    alias(libs.plugins.ksp) apply false
    alias(libs.plugins.naviagation) apply false
    alias(libs.plugins.google.services) apply false

    alias(libs.plugins.kotlin.jvm) apply false
    alias(libs.plugins.doka) apply false
}
apply from: 'tools.gradle'
apply from: 'integration-test.gradle'
apply from: 'ext_settings.gradle'

allprojects {
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs << "-Xlint:deprecation,unchecked"
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    tasks.withType(KotlinCompile).configureEach {
//        kotlinOptions.jvmTarget = "17"
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_17)
            freeCompilerArgs.add("-java-parameters")
        }
    }
}

subprojects {
    plugins.withId("org.jetbrains.kotlin.jvm") {
        kotlin {
            jvmToolchain {
                languageVersion.set(JavaLanguageVersion.of(17))
            }
        }
    }
}

def props = new Properties()
try {
    //this allows having a drive with the keys to be mounted for releases, only.
    props.load(new FileInputStream("keys.properties"))
} catch (Exception ignore) {
}
props.each {
    key, val -> extensions."$key" = val
}